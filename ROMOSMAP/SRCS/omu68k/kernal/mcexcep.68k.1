******************************************************************************
*	Exception.68k	Exception and Interupt handler dispatcher, entry points
******************************************************************************
*
*		All of the 68000 exception vectors are set to point
*	into the dispatch table. If the dispatch table is less than
*	the 256	entries required to accomplish this then all vectors
*	above will be set to point to the last entry in the dispatch
*	table.
*		The dispatch table consisits of a sequence of BSR.W
*	instructions which will place on the stack their address in
*	the dispatch table +4, and the jump to the appropriate wrapper
*	for this exception.
*	Note these wrappers must be within +- 32K bytes of the BSR.W.


******************************************************************************
* Interrupt vector dispatch table
* One entry per interrupt vector location
******************************************************************************
*

	text
	globl  _dispatc, _wrapper

_dispatc:
	bsr.w	lfault		; 0	Reset: Initial SSP
	bsr.w	lfault		; 1	Reset: Initial PC
	bsr.w	lfault		; 2	Bus Error
	bsr.w	lfault		; 3	Address Error
	bsr.w	lfault		; 4	Illegal Instruction
	bsr.w	lfault		; 5	Zero Divide
	bsr.w	lfault		; 6	CHK Instruction
	bsr.w	lfault		; 7	TRAPV Instruction
	bsr.w	lfault		; 8	Privilege Violation
	bsr.w	lfault		; 9	Trace
	bsr.w	lfault		; 10	Line 1010 Emulator
	bsr.w	lfault		; 11	Line 1111 Emulator
	bsr.w	lfault		; 12	(Unassigned, reserved)
	bsr.w	lfault		; 13	(Unassigned, reserved)
	bsr.w	lfault		; 14	(Unassigned, reserved)
	bsr.w	lfault		; 15	(Unassigned, reserved)
	bsr.w	lfault		; 16	(Unassigned, reserved)
	bsr.w	lfault		; 17	(Unassigned, reserved)
	bsr.w	lfault		; 18	(Unassigned, reserved)
	bsr.w	lfault		; 19	(Unassigned, reserved)
	bsr.w	lfault		; 20	(Unassigned, reserved)
	bsr.w	lfault		; 21	(Unassigned, reserved)
	bsr.w	lfault		; 22	(Unassigned, reserved)
	bsr.w	lfault		; 23	(Unassigned, reserved)
	bsr.w	lfault		; 24	Spurious Interrupt
	bsr.w	lfault		; 25	Level 1 Interrupt Autovector
	bsr.w	lfault		; 26	Level 2 Interrupt Autovector
	bsr.w	lfault		; 27	Level 3 Interrupt Autovector
	bsr.w	lfault		; 28	Level 4 Interrupt Autovector
	bsr.w	lfault		; 29	Level 5 Interrupt Autovector
	bsr.w	lfault		; 30	Level 6 Interrupt Autovector
	bsr.w	lfault		; 31	Level 7 Interrupt Autovector
	bsr.w	lsys		; 32	TRAP Instruction Vector 0 system call
	bsr.w	lpswtch		; 33	TRAP Instruction Vector 1 process swtch
	bsr.w	lsuper		; 34	TRAP Instruction Vector 2 to super state
	bsr.w	lfault		; 35	TRAP Instruction Vector 3
	bsr.w	lfault		; 36	TRAP Instruction Vector 4
	bsr.w	lfault		; 37	TRAP Instruction Vector 5
	bsr.w	lfault		; 38	TRAP Instruction Vector 6
	bsr.w	lfault		; 39	TRAP Instruction Vector 7
	bsr.w	lfault		; 40	TRAP Instruction Vector 8
	bsr.w	lfault		; 41	TRAP Instruction Vector 9
	bsr.w	lfault		; 42	TRAP Instruction Vector 10
	bsr.w	lfault		; 43	TRAP Instruction Vector 11
	bsr.w	lfault		; 44	TRAP Instruction Vector 12
	bsr.w	lfault		; 45	TRAP Instruction Vector 13
	bsr.w	lfault		; 46	TRAP Instruction Vector 14
	bsr.w	lfault		; 47	TRAP Instruction Vector 15
	bsr.w	intacia		; 48	(Unassigned, reserved) acia interupt
	bsr.w	lfault		; 49	(Unassigned, reserved)
	bsr.w	lfault		; 50	(Unassigned, reserved)
	bsr.w	lfault		; 51	(Unassigned, reserved)
	bsr.w	lfault		; 52	(Unassigned, reserved)
	bsr.w	lfault		; 53	(Unassigned, reserved)
	bsr.w	lfault		; 54	(Unassigned, reserved)
	bsr.w	lfault		; 55	(Unassigned, reserved)
	bsr.w	lfault		; 56	(Unassigned, reserved)
	bsr.w	lfault		; 57	(Unassigned, reserved)
	bsr.w	lfault		; 58	(Unassigned, reserved)
	bsr.w	lfault		; 59	(Unassigned, reserved)
	bsr.w	lfault		; 60	(Unassigned, reserved)
	bsr.w	lfault		; 61	(Unassigned, reserved)
	bsr.w	lfault		; 62	(Unassigned, reserved)
	bsr.w	lfault		; 63	(Unassigned, reserved)


******************************************************************************
*	Lfault	Fault wrapper,	Calls C routine trap() with exception
*				number as argument
******************************************************************************

	globl	_trap

lfault:
	subi.l	#_dispatc+4,(a7);Gets offset into dispatch table
	lsr.w	2(a7)		;Divides number by 4 to get trap number
	lsr.w	2(a7)
	move.l	(a7),d0		;Saves argument in d0
	move.l	#_trap,(a7)	;Saves function address on stack
	jsr	_wrapper	;Jumps to common wrapper
	addq.l	#4,a7
	rte

******************************************************************************
*	Lsys	system trap call,	Jumps to system call wrapper
******************************************************************************

	globl	_sys

lsys:
	move.l	#_sys,(a7)	;Saves function address over dispatch address
	jsr	_wrapper	;Jump to system wrapper
	addq.l	#4,a7		;Recover stack
	rte

******************************************************************************
*	Lswap	system Concurent swap call,	Switches to new process
******************************************************************************

	globl	_pswtch

lpswtch:
	move.l	#_pswtch,(a7)	;Saves function address over dispatch address
	jsr	_wrapper	;Jump to system wrapper
	addq.l	#4,a7		;Recover stack
	rte

********************************************************************************
*	Lsuper	Sets super state
********************************************************************************

lsuper:
	addq.l	#4,a7		;Ignore dispatch address
	ori.w	#0x2000,(a7)	;Sets super bit in status word on stack
	rte

******************************************************************************
*	Intacia		Acia interupt	Jumps to ACIA interupt address
*			in tty handler
******************************************************************************

	globl	_i_acia

intacia:
	addq.l #4,a7		;Remove dispatch table address information */
	movem.l d0-a6,-(a7)
	jsr _i_acia
	movem.l (a7)+,d0-a6
	rte

******************************************************************************
*	Inttick		20ms Timmer interupt
******************************************************************************

	globl	_tick

;Old tick call does not save processor state
inttick:
	addq.l #4,a7
	movem.l d0-a6,-(a7)
	jsr _tick
	movem.l (a7)+,d0-a6
	rte

;inttick:
;	move.l	#_tick,(a7)	;Saves function address over dispatch address
;	jsr	_wrapper	;Jump to system wrapper
;	addq.l	#4,a7		;Recover stack
;	rte

*	Codata call for reference
*clock:  movl    #_clock,sp@     ; push call address
*	clrw    sp@-            ; device number
*	jmp     call            ; jump to common interrupt handler



******************************************************************************
*	Miscalainious routines
******************************************************************************
*

	globl	_getstack, _setstack, _getusp, _setusp, _getst, _setsr
	globl	_spl7,	_spl6,	_spl5,	_spl4,	_spl3,	_spl2
	globl	_spl1,	_spl0,	_splx

_getstack:
	move.l a7,d0
	rts

_setstack:
	movea.l	(a7),a6		;Saves new sp in a6
	move.l (a7)+,-(a6)	;Saves old return address onto new stack
	movea.l a6,a7		;Sets to new sp
	rts

_getusp:
	move.l	a0,-(a7)	;Saves a0
	move.l	usp,a0		;Gets user stack pointer
	move.l	a0,d0
	addq.l	#4,a7		;Recovers stack
	rts

_setusp:
	move.l	a0,-(a7)	;Saves a0
	movea.l	8(a7),a0	;Sets user stack pointer
	move.l	a0,usp
	addq.l	#4,a7		;Recovers stack
	rts

_getsr:
	move.w sr,d0		;Gets status information
	rts

_setsr:
	move.w 6(a7),sr		;Sets status register
	rts

*
*	Sets interupt levels	returns present status register
*				for use with splx.

_spl7:				;Sets NMI
	move.w sr,d0
	move.w #$2700,sr
	rts

_spl6:				;Sets NMI as well
	move.w sr,d0
	move.w #$2600,sr
	rts

_spl5:
	move.w sr,d0
	move.w #$2500,sr
	rts

_spl4:
	move.w sr,d0
	move.w #$2400,sr
	rts

_spl3:
	move.w sr,d0
	move.w #$2300,sr
	rts

_spl2:
	move.w sr,d0
	move.w #$2200,sr
	rts

_spl1:
	move.w sr,d0
	move.w #$2100,sr
	rts

_spl0:
	move.w sr,d0
	move.w #$2000,sr
	rts

_splx:				;Returns to interupt level given
	move.l 4(a7),d0
	ori.l #$2000,d0
	move.w d0,sr
	rts



******************************************************************************
*	Intvect		Sets up an interupt vector to point to the required
*			location. Codata C interface,
*			<vector (0x40 ..)><address pointing to>
******************************************************************************

	globl	_intvect

_intvect:
	link a6,#-4
	movem.l a5,-4(a6)
	move.l 8(a6),d0
	asl.l #2,d0
	movea.l d0,a5
	move.l 12(a6),(a5)
	movem.l -4(a6),a5
	unlk a6
	rts

******************************************************************************
*	Sets CPU status ( Led reads 7, 20ms tick on)
******************************************************************************
*

STATUSLED: = 0xFA0100

	globl	_s_init;

_s_init:
	move.w #$0718,STATUSLED
	rts


